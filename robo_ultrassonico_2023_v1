/*************************************************************/
/* Tema - Robô Ultrassônico                                  */
/* Programação do projeto robô ultrassônico.                 */
/* Ao transferir o código abaixo para seu Arduino, o robô    */
/* será capaz de de mover para frente e ao se aproximar de   */
/* obstáculo, irá realizar uma manobra para desviá-lo.       */
/* Links para obtenção da biblioteca by Andrea Lombardo      */
/* http://librarymanager/All#L298N#EASY                      */
/* https://github.com/AndreaLombardo/L298N                   */
/* Links para obtenção da biblioteca by Erick Simões         */
/* http://librarymanager/All#minimalist#Ultrasonic           */
/* https://github.com/ErickSimoes/Ultrasonic                 */
/*************************************************************/

#include <L298NX2.h>
#include <Ultrasonic.h>
/* Define os pinos para o sensor ultrassônico.              */
#define pino_Trig 11
#define pino_Echo 12
/* Define a potência dos motores: 0 à 255.                  */
int potencia = 200;

#define ENA 10 // ENA precisa estar em uma porta PWM
#define IN1 9
#define IN2 8
#define IN3 7
#define IN4 6
#define ENB 5  // ENB precisa estar em uma porta PWM

/* Criamos um objetro de controle dos motores.              */
L298NX2 motores(ENA, IN1, IN2, ENB, IN3, IN4);

/* Cria o objeto Sensor que realizará a leitura do sensor.  */
Ultrasonic Sensor(pino_Trig, pino_Echo);
/* Variável que armazenará as medidas.                      */
int distancia;
void setup() {
  /* Inicia com os motores parados.                         */
  motores.stop();
  /* Espera 3 segundos para iniciar o movimento do robô.    */
  delay(3000);
}
/* Função que move o robô para Frente.                      */
void frente(int potencia)
{
  /* Define a velocidade dos motores 255 (0-255).           */
  motores.setSpeedA(potencia);
  motores.setSpeedB(potencia);
  /* Aciona os Motores para frente.                         */
  motores.forward();
}
/* Função que manobra o robô com 60% de sua velocidade.     */
void manobra(int potencia)
{
  motores.setSpeedA(potencia * 0.6);
  motores.setSpeedB(potencia * 0.6);
  /* Para os Motores.                                  */
  motores.stop();
  delay(500);
  /* Gira no sentido horário.                          */
  motores.forwardA();
  motores.backwardB();
  delay(1000);
  /* Para os Motores.                                  */
  motores.stop();
  delay(500);
}
void loop() {
  /* Armazena a distância na variável "distancia".          */
  distancia = Sensor.read();
  /* Se a distância for menor ou igual a 40cm, realize a    */
  /* manobra do robô.                                       */
  if (distancia <= 40)
  {
    /* Chama a função responsável pela manobra.             */
    manobra(potencia);
  }
  /* Senão siga em frente.                                  */
  else
  {
    /* Chama a função mover para frente.                    */
    frente(potencia);
  }
}
